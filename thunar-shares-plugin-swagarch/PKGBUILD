# Maintainer : Mike Kr√ºger <mikekrueger81@gmail.com>

_name=thunar-shares-plugin
pkgname=thunar-shares-plugin-swagarch
pkgver=0.2.0.git
pkgrel=6
pkgdesc="Thunar plugin to quickly share a folder using Samba without requiring root access (with auto-setup)"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/thunar-plugins/${_name}"
license=('GPL2' 'LGPL')
depends=('thunar' 'samba-patched')
makedepends=('xfce4-dev-tools')
backup=('etc/samba/smb.conf')
provides=("${_name}=${pkgver}")
conflicts=("${_name}")
install="${pkgname}.install"
source=("git://git.xfce.org/thunar-plugins/thunar-shares-plugin"
        "smb.conf")
sha1sums=('SKIP'
          '9edd4e9d59636b1b32436e55c5a030c004784fb5')

build() {
    cd "${srcdir}/${_name}"
    ./autogen.sh --prefix=/usr --enable-debug=no
    make
}

package() {
    cd "${srcdir}/${_name}"
    make DESTDIR="${pkgdir}" install
  
    install -Dm644 "${srcdir}/smb.conf" "${pkgdir}/etc/samba/smb.conf"
    mkdir -p "${pkgdir}/var/lib/samba/usershare"
    chmod 1775 "${pkgdir}/var/lib/samba/usershare"
}

